apply plugin: 'maven-publish'
apply plugin: 'java'

version = "3.0.5"
group= "dev.gigaherz.graph" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
ext {
    archivesBaseName = "GraphLib3"
}

repositories {
    mavenCentral()
}
dependencies {
    compileOnly 'org.jetbrains:annotations:20.1.0'
    implementation 'com.google.guava:guava:21.0'
}

java {
    withSourcesJar()
}

jar {
    manifest {
        attributes([
            "Specification-Title": "graphlib",
            "Specification-Vendor": "gigaherz",
            "Specification-Version": "3",
            "Implementation-Title": project.name,
            "Implementation-Version": "${project.version}",
            "Implementation-Vendor" :"gigaherz",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
            "Maven-Artifact":"${project.group}:${project.archivesBaseName}:${project.version}",
            "FMLModType":"GAMELIBRARY"
        ])
    }
}

tasks.publish.dependsOn build
publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId project.archivesBaseName
            from components.java
        }
    }

    repositories {
        if (findProperty("RELEASE") && System.env.giga_maven_host != null) {
            System.out.println("Remote publish enabled on " + System.env.giga_maven_host)
            maven {
                url System.env.giga_maven_host
                credentials {
                    username System.env.giga_maven_user
                    password System.env.giga_maven_password
                }
            }
        }
        else {
            System.out.println("Remote publish disabled.")
            maven {
                url "$projectDir/../CommonMaven"
            }
        }
    }
}
